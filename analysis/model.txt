model{
#abundance model
  #likelihood
  for(i in 1:nSurveys){
      N[i] ~ dpois(lambda[i])
      log(lambda[i]) <- mu + siteRan[site[i]] + yearRan[year[i]] + eps[site[i],year[i]] + log(siteLength[i])
  }
  
  #priors
  mu ~ dnorm(0,0.01)
  
  siteTau<-1/pow(siteSigma,-2)
  siteSigma ~ dunif(0,10)
  
  yearTau<-1/pow(yearSigma,-2)
  yearSigma ~ dunif(0,10)
  
  epsTau<-1/pow(epsSigma,-2)
  epsSigma ~ dunif(0,10)
  
  for(t in 1:nYears){
    yearRan[t]~dnorm(0,yearTau)
  }
  for(s in 1:nSites){
    siteRan[s]~dnorm(0,siteTau)
  }
  for(t in 1:nYears){
    for(s in 1:nSites){
      eps[s,t]~dnorm(0,epsTau)
    }
  }
  
#observation model
  #likelihood
  for(i in 1:nFirstPassRows){
    y[firstPassRows[i]]~dbin(pDep[firstPassRows[i]],N[survey[firstPassRows[i]]])
  }
  for(i in 1:nSecondPassRows){
    y[secondPassRows[i]]~dbin(pDep[secondPassRows[i]],N[survey[secondPassRows[i]]]-y[secondPassRows[i]-1])
  }
  for(i in 1:nThirdPassRows){
    y[thirdPassRows[i]]~dbin(pDep[thirdPassRows[i]],N[survey[thirdPassRows[i]]]-
                                                    y[thirdPassRows[i]-1]-
                                                    y[thirdPassRows[i]-2])
  }
  for(i in 1:nFourthPassRows){
    y[fourthPassRows[i]]~dbin(pDep[fourthPassRows[i]],N[survey[fourthPassRows[i]]]-
                                                      y[fourthPassRows[i]-1]-
                                                      y[fourthPassRows[i]-2]-
                                                      y[fourthPassRows[i]-3])
  }
  
  for(i in 1:nRecapRows){
    y[recapRows[i]]~dbin(pMr[recapRows[i]],y[recapRows[i]-2]) #1st pass captures are two rows above recaps
  }
  for(i in 1:nUnmarkedRows){
    y[unmarkedRows[i]]~dbin(pMr[recapRows[i]],N[survey[unmarkedRows[i]]]-y[unmarkedRows[i]-1])
  }
  

  for(i in 1:nDepRows){
    logitPDep[depRows[i]]<-muPDep + pDepBetaWidth*siteWidth[depRows[i]] +
                            pDepBetaFlow*typicalFlow[dayOfYear[depRows[i]]] + pDepEps[site[depRows[i]]]
    pDep[depRows[i]]<-1/(1+exp(-logitPDep[depRows[i]]))
  }

  for(i in 1:nMrRows){
    logitPMr[mrRows[i]]<-muPMr + pMrBetaWidth*siteWidth[mrRows[i]] + 
                          pMrBetaFlow*typicalFlow[dayOfYear[mrRows[i]]]+ pMrEps[site[mrRows[i]]]
    pMr[mrRows[i]]<-1/(1+exp(-logitPMr[mrRows[i]]))
  }
  
  #priors
  muPMr~dnorm(0,0.01)
  pMrTau<-1/pow(pMrSigma,-2)
  pMrSigma ~ dunif(0,10)
  pMrBetaWidth~dnorm(0,0.01)
  pMrBetaFlow~dnorm(0,0.01)  

  for(s in 1:nMrSites){
    pMrEps[mrSites[s]]~dnorm(0,pMrTau)
  }

  muPDep~dnorm(0,0.01)
  pDepTau<-1/pow(pDepSigma,-2)
  pDepSigma ~ dunif(0,10)
  pDepBetaWidth~dnorm(0,0.01)
  pDepBetaFlow~dnorm(0,0.01)

  for(s in 1:nDepSites){
    pDepEps[depSites[s]]~dnorm(0,pDepTau)
  }

}